plugins {
    id "com.starter.easylauncher" version "4.0.0"
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply from: "${rootDir}/gradle/build-script.gradle"

android {
    compileSdkVersion compile_sdk_version
    defaultConfig {
        applicationId "tw.com.louis383.coffeefinder"
        targetSdkVersion target_sdk_version
        minSdkVersion min_sdk_version
        versionCode getVersionCodeTimeStamps()
        versionName app_version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', 'HOST', APIHost
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    signingConfigs {
        release {
            storeFile file(keystorePath)
            keyAlias keystoreAlias
            storePassword storePass
            keyPassword keyPass
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            manifestPlaceholders = [ googleMapKeys: googleMapKey_debug ]
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [ googleMapKeys: googleMapKey_release ]
        }
    }
}

easylauncher {
    iconNames = ["@mipmap/ic_launcher"]
    buildTypes {
        debug {
            filters = [ orangeRibbonFilter("EXP") ]
        }
    }
}

androidExtensions {
    experimental = true
}

ext {
    retrofit_version = '2.8.1'
    coroutines_version = '1.3.9'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":anchor-bottom-sheet-behavior")
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'
    // FIXME:: map 3.1.0 use volley 1.1.0 which is not available on mavenCentral and google
    //  so we include it explicitly
    implementation 'com.google.maps.android:maps-v3-ktx:3.1.0'
    implementation 'com.google.android.libraries.maps:maps:3.1.0-beta'
    implementation 'com.android.volley:volley:1.2.0'
    // ==== ==== ==== ==== ==== ==== ==== ==== ==== ==== ==== ====

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutines_version}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${coroutines_version}"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:1.0.0-RC"

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation "androidx.fragment:fragment-ktx:1.2.5"
    implementation "com.google.android.gms:play-services-location:${gms_version}"

    // HTTP Client
    implementation "com.squareup.retrofit2:retrofit:${retrofit_version}"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.7.0"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.5.0'

    // Dependency Injection
    implementation 'com.google.dagger:dagger:2.38.1'
    kapt 'com.google.dagger:dagger-compiler:2.38.1'

    // ViewPager Indicator
    // FIXME:: Fetch on JitPack.io, version 1.0.3
    implementation 'com.github.romandanylyk:PageIndicatorView:b1bad589b5'

    // for unit test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
repositories {
    mavenCentral()
}
