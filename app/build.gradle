plugins {
    id "com.starter.easylauncher" version "4.0.0"
    id "io.gitlab.arturbosch.detekt" version "1.17.1"
    id "org.jlleitschuh.gradle.ktlint" version "10.1.0"
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply from: "${rootDir}/gradle/build-script.gradle"

android {
    compileSdkVersion compile_sdk_version
    defaultConfig {
        applicationId "tw.com.louis383.coffeefinder"
        targetSdkVersion target_sdk_version
        minSdkVersion min_sdk_version
        versionCode getVersionCodeTimeStamps()
        versionName app_version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    signingConfigs {
        local {
            storeFile file(debugKeystorePath)
            keyAlias debugKeystoreAlias
            storePassword debugKeyPass
            keyPassword debugStorePass
        }
        release {
            storeFile file(keystorePath)
            keyAlias keystoreAlias
            storePassword storePass
            keyPassword keyPass
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.local
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            manifestPlaceholders = [ googleMapKeys: googleMapKey_debug ]
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [ googleMapKeys: googleMapKey_release ]
        }
    }

    lintOptions {
        checkDependencies true
    }
}

easylauncher {
    iconNames = ["@mipmap/ic_launcher"]
    buildTypes {
        debug {
            filters = [ orangeRibbonFilter("EXP") ]
        }
    }
}

detekt {
    buildUponDefaultConfig = true // preconfigure defaults
    allRules = false // activate all available (even unstable) rules.
    config = files("$projectDir/config/detekt.yml") // point to your custom config defining rules to run, overwriting default behavior
    baseline = file("$projectDir/config/baseline.xml") // a way of suppressing issues before introducing detekt

    reports {
        html.enabled = true // observe findings in your browser with structure and code snippets
        xml.enabled = true // checkstyle like format mainly for integrations like Jenkins
        txt.enabled = true // similar to the console output, contains issue signature to manually edit baseline files
        sarif.enabled = true // standardized SARIF format (https://sarifweb.azurewebsites.net/) to support integrations with Github Code Scanning
    }
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
    jvmTarget = "1.8"
}

ext {
    retrofit_version = '2.9.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":core")
    implementation project(":anchor-bottom-sheet-behavior")
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation 'com.google.android.gms:play-services-maps:18.0.0'
    implementation "com.google.android.gms:play-services-location:${gms_version}"
    // ==== ==== ==== ==== ==== ==== ==== ==== ==== ==== ==== ====

    implementation platform("org.jetbrains.kotlinx:kotlinx-coroutines-bom:1.5.1")
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services"

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.5.0-beta01'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "androidx.core:core-splashscreen:1.0.0-alpha02"
    implementation "androidx.fragment:fragment-ktx:1.3.6"

    // HTTP Client
    implementation "com.squareup.retrofit2:retrofit:${retrofit_version}"

    // Dependency Injection
    implementation "com.google.dagger:hilt-android:$dagger_version"
    kapt "com.google.dagger:hilt-compiler:$dagger_version"

    // ViewPager Indicator
    // FIXME:: Fetch on JitPack.io, version 1.0.3
    implementation 'com.github.romandanylyk:PageIndicatorView:b1bad589b5'

    detektPlugins("io.gitlab.arturbosch.detekt:detekt-formatting:1.17.1")

    // for unit test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
repositories {
    mavenCentral()
}
